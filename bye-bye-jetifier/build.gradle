ext.PROJECT_NAME = "Bye bye Jetifier"
description = "Gradle Plugin to verify if you can keep Android Jetifier disabled"
ext.ARTIFACT_ID = "bye-bye-jetifier"

boolean isCI = System.getenv("CI") == "true"
if (isCI) {
	ext["signing.keyId"] = System.getenv('SIGNING_KEY_ID')
	ext["signing.password"] = System.getenv('SIGNING_PASSWORD')
	ext["signing.secretKeyRingFile"] = System.getenv('SIGNING_SECRET_KEY_RING_FILE')
	ext["gradle.publish.key"] = System.getenv('GRADLE_PUBLISH_KEY')
	ext["gradle.publish.secret"] = System.getenv('GRADLE_PUBLISH_SECRET')
}

apply plugin: "com.jdroidtools.gradle.plugin"
apply plugin: "com.gradle.plugin-publish"

buildscript {
	dependencies {
		classpath(BuildLibs.JDROID_GRADLE_PROJECT_PLUGIN)
		classpath(BuildLibs.GRADLE_PLUGIN_PUBLISH_PLUGIN)
	}
}

dependencies {
	implementation(Libs.ASM)
	implementation(Libs.ASM_UTILS)
	implementation(Libs.ASM_COMMONS)
	implementation(Libs.GSON)
	testImplementation(Libs.JUNIT)
}

if (project.hasProperty("GRADLE_PLUGIN_PORTAL_ENABLED") && project.property("GRADLE_PLUGIN_PORTAL_ENABLED") == "true") {
	gradlePlugin {
		plugins {
			byeByeJetifierPlugin {
				id = "com.dipien.byebyejetifier"
				implementationClass = "com.dipien.byebyejetifier.ByeByeJetifierGradlePlugin"
			}
		}
	}

	pluginBundle {
		website = "https://byebyejetifier.dipien.com/"
		vcsUrl = "https://github.com/dipien/bye-bye-jetifier"
		description = "Gradle Plugin to verify if you can keep Android Jetifier disabled"
		tags = ["gradle", "plugin", "jetifier", "android"]

		plugins {
			byeByeJetifierPlugin {
				displayName = "Bye bye Jetifier"
			}
		}

		mavenCoordinates {
			groupId = "com.dipien"
			artifactId = ext.ARTIFACT_ID
		}
	}
}
